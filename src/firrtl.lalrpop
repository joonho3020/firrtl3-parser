use rusty_firrtl::Int;
use crate::lexer::*;

grammar;

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    "indent"           => Token::Indent,
    "dedent"           => Token::Dedent,
    "info"             => Token::Info(<String>),
    "annotations"      => Token::Annotations(<String>),
    "id"               => Token::ID(<Int>),
    " "                => Token::Space,
    "\t"               => Token::Tab,
    "\n"               => Token::Newline,
    "radixint"         => Token::RadixInt(<String>),
    "int"              => Token::IntegerDec(<Int>),
    "identifier"       => Token::Identifier(<String>),
    "string"           => Token::String(<String>),
    "/"                => Token::Slash,
    "["                => Token::LeftSquare,
    "]"                => Token::RightSquare,
    "<"                => Token::LeftAngle,
    ">"                => Token::RightAngle,
    "{"                => Token::LeftBracket,
    "}"                => Token::RightBracket,
    "("                => Token::LeftParenthesis,
    ")"                => Token::RightParenthesis,
    "@"                => Token::AtSymbol,
    "`"                => Token::Backtick,
    "%[["              => Token::AnnoStart,
    "<<"               => Token::DoubleLeft,
    ">>"               => Token::DoubleRight,
    "clock"            => Token::Clock,
    "reset"            => Token::Reset,
    "async_reset"      => Token::AsyncReset,
    "uint"             => Token::UInt,
    "sint"             => Token::SInt,
    "probe_type"       => Token::ProbeType,
    "probe"            => Token::Probe,
    "analog"           => Token::Analog,
    "fixed"            => Token::Fixed,
    "flip"             => Token::Flip,
    "primop_e2"        => Token::E2Op(<String>),
    "primop_e1"        => Token::E1Op(<String>),
    "primop_e1i1"      => Token::E1I1Op(<String>),
    "primop_e1i2"      => Token::E1I2Op(<String>),
    "mux"              => Token::Mux,
    "validif"          => Token::ValidIf,
    "mem"              => Token::Mem,
    "smem"             => Token::SMem,
    "cmem"             => Token::CMem,
    "write"            => Token::Write,
    "read"             => Token::Read,
    "infer"            => Token::Infer,
    "mport"            => Token::Mport,
    "datatype"         => Token::DataType,
    "depth"            => Token::Depth,
    "read_lat"         => Token::ReadLatency,
    "write_lat"        => Token::WriteLatency,
    "read_under_write" => Token::ReadUnderWrite,
    "reader"           => Token::Reader,
    "writer"           => Token::Writer,
    "readwriter"       => Token::ReadWriter,
    "wire"             => Token::Wire,
    "reg"              => Token::Reg,
    "reg_reset"        => Token::RegReset,
    "inst"             => Token::Inst,
    "of"               => Token::Of,
    "node"             => Token::Node,
    "is"               => Token::Is,
    "invalid"          => Token::Invalidate,
    "attach"           => Token::Attach,
    "when"             => Token::When,
    "else"             => Token::Else,
    "stop"             => Token::Stop,
    "printf"           => Token::Printf,
    "assert"           => Token::Assert,
    "skip"             => Token::Skip,
    "input"            => Token::Input,
    "output"           => Token::Output,
    "module"           => Token::Module,
    "extmodule"        => Token::ExtModule,
    "intmodule"        => Token::IntModule,
    "defname"          => Token::DefName,
    "parameter"        => Token::Parameter,
    "intrinsic"        => Token::Intrinsic,
    "FIRRTL"           => Token::FIRRTL,
    "version"          => Token::Version,
    "circuit"          => Token::Circuit,
    "<="               => Token::Connect,
    "=>"               => Token::SetValue,
    "with"             => Token::With,
    "public"           => Token::Public,
    "define"           => Token::Define,
    "const"            => Token::Const,
    "symbol"           => Token::Symbol(<String>),
    "."                => Token::Period,
  }
}

pub Float: rusty_firrtl::Float = {
    <integer: "int"> "."  <decimal: "id"> => rusty_firrtl::Float::new(integer.to_u32(), decimal.to_u32()),
};

pub Info: rusty_firrtl::Info = {
  <info: "info"> => rusty_firrtl::Info(info)
};

// TODO: Add radint case
pub Width: rusty_firrtl::Width = {
    "<" <w: "int"> ">" => rusty_firrtl::Width(w.to_u32()),
};

pub TypeGround: rusty_firrtl::TypeGround = {
    "reset" => rusty_firrtl::TypeGround::Reset,
    "clock" => rusty_firrtl::TypeGround::Clock,
    "async_reset" => rusty_firrtl::TypeGround::AsyncReset,
    "uint" <w: Width> => rusty_firrtl::TypeGround::UInt(Some(w)),
    "uint"            => rusty_firrtl::TypeGround::UInt(None),
    "sint" <w: Width> => rusty_firrtl::TypeGround::SInt(Some(w)),
    "sint"            => rusty_firrtl::TypeGround::SInt(None),
};

pub Fields: rusty_firrtl::Fields = {
    <mut head: Fields> "symbol" <tail: Field> => { head.push(Box::new(tail)); head } ,
    <head: Field> => vec![Box::new(head)],
};

pub Field: rusty_firrtl::Field = {
    <name: Identifier> "symbol" <tpe: Type> => rusty_firrtl::Field::Straight(name, Box::new(tpe)),
    "flip" <name: Identifier> "symbol" <tpe: Type> => rusty_firrtl::Field::Flipped(name, Box::new(tpe)),
};

pub TypeAggregate: rusty_firrtl::TypeAggregate = {
    "{" "}" => rusty_firrtl::TypeAggregate::Fields(Box::new(vec![])),
    "{" <fields: Fields> "}" => rusty_firrtl::TypeAggregate::Fields(Box::new(fields)),
    <tpe: Type> "[" <i: "int"> "]"  => rusty_firrtl::TypeAggregate::Array(Box::new(tpe), i),
    <tpe: Type> "[" <i: "id"> "]"  => rusty_firrtl::TypeAggregate::Array(Box::new(tpe), i),
};

pub Type: rusty_firrtl::Type = {
  <tg: TypeGround> => rusty_firrtl::Type::TypeGround(tg),
  // "const" <tg: TypeGround> => rusty_firrtl::Type::ConstTypeGround(tg),
  <ta: TypeAggregate> => rusty_firrtl::Type::TypeAggregate(Box::new(ta)),
  // "const" <ta: TypeAggregate> => rusty_firrtl::Type::ConstTypeAggregate(Box::new(ta)),
};

pub Identifier: rusty_firrtl::Identifier = {
    <id: "id">    => rusty_firrtl::Identifier::ID(id),
    "inst"        => rusty_firrtl::Identifier::Name("inst".to_string()),
    "printf"      => rusty_firrtl::Identifier::Name("printf".to_string()),
    "assert"      => rusty_firrtl::Identifier::Name("assert".to_string()),
    "smem"        => rusty_firrtl::Identifier::Name("smem".to_string()),
    "cmem"        => rusty_firrtl::Identifier::Name("cmem".to_string()),
    "of"          => rusty_firrtl::Identifier::Name("of".to_string()),
    "reg"         => rusty_firrtl::Identifier::Name("reg".to_string()),
    "input"       => rusty_firrtl::Identifier::Name("input".to_string()),
    "output"      => rusty_firrtl::Identifier::Name("output".to_string()),
    "invalid"     => rusty_firrtl::Identifier::Name("invalid".to_string()),
    "mux"         => rusty_firrtl::Identifier::Name("mux".to_string()),
    "stop"        => rusty_firrtl::Identifier::Name("stop".to_string()),
    "depth"       => rusty_firrtl::Identifier::Name("depth".to_string()),
    "skip"        => rusty_firrtl::Identifier::Name("skip".to_string()),
    "write"       => rusty_firrtl::Identifier::Name("write".to_string()),
    "read"        => rusty_firrtl::Identifier::Name("read".to_string()),
    "version"     => rusty_firrtl::Identifier::Name("version".to_string()),
    "probe"       => rusty_firrtl::Identifier::Name("probe".to_string()),
    "module"      => rusty_firrtl::Identifier::Name("module".to_string()),
    "const"       => rusty_firrtl::Identifier::Name("const".to_string()),
    <x: "primop_e2"> => rusty_firrtl::Identifier::Name(x),
    <x: "primop_e1"> => rusty_firrtl::Identifier::Name(x),
    <x: "primop_e1i1"> => rusty_firrtl::Identifier::Name(x),
    <identifier: "identifier"> => rusty_firrtl::Identifier::Name(identifier),
};

pub PrimOp2Expr: rusty_firrtl::PrimOp2Expr = {
    <op: "primop_e2"> "(" => rusty_firrtl::PrimOp2Expr::from(op),
};

pub PrimOp1Expr: rusty_firrtl::PrimOp1Expr = {
    <op: "primop_e1"> "(" => rusty_firrtl::PrimOp1Expr::from(op),
};

pub PrimOp1Expr1Int: rusty_firrtl::PrimOp1Expr1Int = {
    <op: "primop_e1i1"> "(" => rusty_firrtl::PrimOp1Expr1Int::from(op),
};

pub PrimOp1Expr2Int: rusty_firrtl::PrimOp1Expr2Int = {
    <op: "primop_e1i2"> => rusty_firrtl::PrimOp1Expr2Int::from(op),
};

pub Exprs: rusty_firrtl::Exprs = {
    <mut head: Exprs> "symbol" <tail: Expr> => { head.push(Box::new(tail)); head },
    <head: Expr> => vec![Box::new(head)],
};

pub Expr: rusty_firrtl::Expr = {
    <u: "uint"> <w: Width> "(" ")" => rusty_firrtl::Expr::UIntNoInit(w),
    <u: "uint"> <w: Width> "(" <i: "int"> ")" => rusty_firrtl::Expr::UIntInit(w, i),
    <u: "uint"> <w: Width> "(" <ri: "string"> ")" => rusty_firrtl::Expr::UIntInit(w, rusty_firrtl::Expr::parse_radixint(&ri).expect("parse_radixint")),
    <s: "sint"> <w: Width> "(" ")" => rusty_firrtl::Expr::SIntNoInit(w),
    <s: "sint"> <w: Width> "(" <i: "int"> ")" => rusty_firrtl::Expr::SIntInit(w, i),
    <s: "sint"> <w: Width> "(" <ri: "string"> ")" => rusty_firrtl::Expr::SIntInit(w, rusty_firrtl::Expr::parse_radixint(&ri).expect("parse_radixint")),
    <r: Reference> => rusty_firrtl::Expr::Reference(r),
    <m: "mux"> "(" <sel: Expr> "symbol" <exp_true: Expr> "symbol" <exp_false: Expr> ")" => rusty_firrtl::Expr::Mux(Box::new(sel), Box::new(exp_true), Box::new(exp_false)),
    <vif: "validif"> "(" <if_expr: Expr> "symbol" <expr: Expr> ")" => rusty_firrtl::Expr::ValidIf(Box::new(if_expr), Box::new(expr)),
    <op: PrimOp2Expr>     <e1: Expr> "symbol" <e2: Expr> ")" => rusty_firrtl::Expr::PrimOp2Expr(op, Box::new(e1), Box::new(e2)),
    <op: PrimOp1Expr>     <e1: Expr> ")" => rusty_firrtl::Expr::PrimOp1Expr(op, Box::new(e1)),
    <op: PrimOp1Expr1Int> <e1: Expr> "symbol" <int: "int"> ")" => rusty_firrtl::Expr::PrimOp1Expr1Int(op, Box::new(e1), int),
    <op: PrimOp1Expr2Int> <e1: Expr> "symbol" <int1: "int"> "symbol" <int2: "int"> ")" => rusty_firrtl::Expr::PrimOp1Expr2Int(op, Box::new(e1), int1, int2),

    // TODO: add all other primop cases
    // <op: PrimOp1Expr2Int> <e1: "primop_e1i1"> "symbol" <int1: "int"> "symbol" <int2: "int"> ")" => {
    //   rusty_firrtl::Expr::PrimOp1Expr2Int(op, Box::new(rusty_firrtl::Expr::Reference(rusty_firrtl::Reference::Ref(rusty_firrtl::Identifier::Name(e1)))), int1, int2)
    // },
};

pub ChirrtlMemoryDataType: rusty_firrtl::Type = {
    <tpe: Type> => { tpe }
};

pub ChirrtlMemoryReadUnderWrite: rusty_firrtl::ChirrtlMemoryReadUnderWrite = {
    // FIXME: Properly set this flag
    "read_under_write" "=>" <ruw: Identifier> => rusty_firrtl::ChirrtlMemoryReadUnderWrite::default(),
    "read_under_write" "=>" <ruw: "string"> => rusty_firrtl::ChirrtlMemoryReadUnderWrite::default(),
    "read_under_write" => rusty_firrtl::ChirrtlMemoryReadUnderWrite::default(),
};

pub ChirrtlMemory: rusty_firrtl::ChirrtlMemory = {
    "smem" <name: Identifier> "symbol" <tpe: ChirrtlMemoryDataType> "symbol" <ruw: ChirrtlMemoryReadUnderWrite> <info: Info> => rusty_firrtl::ChirrtlMemory::SMem(name, tpe, Some(ruw), info),
    "smem" <name: Identifier> "symbol" <tpe: ChirrtlMemoryDataType> <info: Info> => rusty_firrtl::ChirrtlMemory::SMem(name, tpe, None, info),
    "cmem" <name: Identifier> "symbol" <tpe: ChirrtlMemoryDataType> <info: Info> => rusty_firrtl::ChirrtlMemory::CMem(name, tpe, info),

    // TODO: add all other primop cases
    // "smem" <name: "primop_e1i1"> "symbol" <tpe: ChirrtlMemoryDataType> "symbol" <ruw: ChirrtlMemoryReadUnderWrite> <info: Info> => rusty_firrtl::ChirrtlMemory::SMem(rusty_firrtl::Identifier::Name(name), tpe, Some(ruw), info),
    // "smem" <name: "primop_e1i1"> "symbol" <tpe: ChirrtlMemoryDataType> <info: Info> => rusty_firrtl::ChirrtlMemory::SMem(rusty_firrtl::Identifier::Name(name), tpe, None, info),
    // "cmem" <name: "primop_e1i1"> "symbol" <tpe: ChirrtlMemoryDataType> <info: Info> => rusty_firrtl::ChirrtlMemory::CMem(rusty_firrtl::Identifier::Name(name), tpe, info),
};

pub ChirrtlMemoryPort: rusty_firrtl::ChirrtlMemoryPort = {
    "write" "mport" <name: Identifier> "symbol" <mem_ref: Identifier> "[" <addr_ref: Expr> "]" "symbol" <clk_ref: Reference> <info: Info> => rusty_firrtl::ChirrtlMemoryPort::Write(name, mem_ref, addr_ref, clk_ref, info),
    "write" "mport" <name: Identifier> "symbol" <mem_ref: Identifier> "[" <addr_ref: Expr> "]" "symbol" <clk_ref: Reference>              => rusty_firrtl::ChirrtlMemoryPort::Write(name, mem_ref, addr_ref, clk_ref, rusty_firrtl::Info::default()),
    "read"  "mport" <name: Identifier> "symbol" <mem_ref: Identifier> "[" <addr_ref: Expr> "]" "symbol" <clk_ref: Reference> <info: Info> => rusty_firrtl::ChirrtlMemoryPort::Read(name, mem_ref, addr_ref, clk_ref, info),
    "read"  "mport" <name: Identifier> "symbol" <mem_ref: Identifier> "[" <addr_ref: Expr> "]" "symbol" <clk_ref: Reference>              => rusty_firrtl::ChirrtlMemoryPort::Read(name, mem_ref, addr_ref, clk_ref, rusty_firrtl::Info::default()),
    "infer" "mport" <name: Identifier> "symbol" <mem_ref: Identifier> "[" <addr_ref: Expr> "]" "symbol" <clk_ref: Reference> <info: Info> => rusty_firrtl::ChirrtlMemoryPort::Infer(name, mem_ref, addr_ref, clk_ref, info),
    "infer" "mport" <name: Identifier> "symbol" <mem_ref: Identifier> "[" <addr_ref: Expr> "]" "symbol" <clk_ref: Reference>              => rusty_firrtl::ChirrtlMemoryPort::Infer(name, mem_ref, addr_ref, clk_ref, rusty_firrtl::Info::default()),
}

pub MemoryPort: rusty_firrtl::MemoryPort = {
    "writer"     "=>" <name: Identifier> => rusty_firrtl::MemoryPort::Write(name),
    "reader"     "=>" <name: Identifier> => rusty_firrtl::MemoryPort::Read(name),
    "readwriter" "=>" <name: Identifier> => rusty_firrtl::MemoryPort::ReadWrite(name),
}

pub MemoryPorts: rusty_firrtl::MemoryPorts = {
    <mut head: MemoryPorts> <tail: MemoryPort> => { head.push(Box::new(tail)); head } ,
    <head: MemoryPort> => vec![Box::new(head)],
}

// pub Refs = Comma<Ref>;

pub Reference: rusty_firrtl::Reference = {
    <name: Identifier> => rusty_firrtl::Reference::Ref(name),
    <r: Reference> "." <idx: Identifier> => rusty_firrtl::Reference::RefDot(Box::new(r), idx),
    <r: Reference> "[" <idx: "int"> "]" => rusty_firrtl::Reference::RefIdxInt(Box::new(r), idx),
    <r: Reference> "[" <idx: Expr> "]" => rusty_firrtl::Reference::RefIdxExpr(Box::new(r), Box::new(idx)),
};


pub Stmts: rusty_firrtl::Stmts = {
    <mut head: Stmts> <tail: Stmt> => { head.push(Box::new(tail)); head } ,
    <head: Stmt> => vec![Box::new(head)],
};

pub Stmt: rusty_firrtl::Stmt = {
  "wire" <name: Identifier> "symbol" <tpe: Type> <info: Info> => rusty_firrtl::Stmt::Wire(name, tpe, info),
  "wire" <name: Identifier> "symbol" <tpe: Type>              => rusty_firrtl::Stmt::Wire(name, tpe, rusty_firrtl::Info::default()),
  "reg" <name: Identifier> "symbol" <tpe: Type> "symbol" <clk: Expr> "with" "symbol" "indent" <reset: Identifier> "=>" "(" <reset_cond: Expr> "symbol" <reset_val: Expr> ")" <info: Info> "dedent" => {
    let rst_expr = rusty_firrtl::Expr::Reference(rusty_firrtl::Reference::Ref(reset.clone()));
    if rst_expr == reset_cond {
      rusty_firrtl::Stmt::RegReset(name, tpe, clk, reset_cond, reset_val, info)
    } else {
      rusty_firrtl::Stmt::Reg(name, tpe, clk, info)
    }
  },
  "reg" <name: Identifier> "symbol" <tpe: Type> "symbol" <clk: Expr> "with" "symbol" "indent" <reset: Identifier> "=>" "(" <reset_cond: Expr> "symbol" <reset_val: Expr> ")"              "dedent" => {
    let rst_expr = rusty_firrtl::Expr::Reference(rusty_firrtl::Reference::Ref(reset.clone()));
    if rst_expr == reset_cond {
      rusty_firrtl::Stmt::RegReset(name, tpe, clk, reset_cond, reset_val, rusty_firrtl::Info::default())
    } else {
      rusty_firrtl::Stmt::Reg(name, tpe, clk, rusty_firrtl::Info::default())
    }
  },
  <m: ChirrtlMemory> => rusty_firrtl::Stmt::ChirrtlMemory(m),
  <mp: ChirrtlMemoryPort> => rusty_firrtl::Stmt::ChirrtlMemoryPort(mp),

  "inst" <inst: Identifier> "of" <module: Identifier> <info: Info> => rusty_firrtl::Stmt::Inst(inst, module, info),
  "inst" <inst: Identifier> "of" <module: Identifier>              => rusty_firrtl::Stmt::Inst(inst, module, rusty_firrtl::Info::default()),

  "node" <name: Identifier> "symbol" <expr: Expr> <info: Info> => rusty_firrtl::Stmt::Node(name, expr, info),
  "node" <name: Identifier> "symbol" <expr: Expr>              => rusty_firrtl::Stmt::Node(name, expr, rusty_firrtl::Info::default()),

  // TODO: add all other primop cases
  // "node" <name: "primop_e1i1"> "symbol" <expr: Expr> <info: Info> => rusty_firrtl::Stmt::Node(rusty_firrtl::Identifier::Name(name), expr, info),

  <sink: Expr> "<=" <driver: Expr> <info: Info> => rusty_firrtl::Stmt::Connect(sink, driver, info),
  <sink: Expr> "<=" <driver: Expr>              => rusty_firrtl::Stmt::Connect(sink, driver, rusty_firrtl::Info::default()),
  <exp: Expr> "is" "invalid" <info: Info> => rusty_firrtl::Stmt::Invalidate(exp, info),
  <exp: Expr> "is" "invalid"              => rusty_firrtl::Stmt::Invalidate(exp, rusty_firrtl::Info::default()),

  "when" <cond: Expr> "symbol" <info: Info> "indent" <stmts_true: Stmts> "dedent" "else" "symbol" "indent" <stmts_false: Stmts> "dedent" => rusty_firrtl::Stmt::When(cond, info, stmts_true, Some(stmts_false)),
  "when" <cond: Expr> "symbol" <info: Info> "indent" <stmts_true: Stmts> "dedent"  => rusty_firrtl::Stmt::When(cond, info, stmts_true, None),

  "when" <cond: Expr> "symbol"              "indent" <stmts_true: Stmts> "dedent" "else" "symbol" "indent" <stmts_false: Stmts> "dedent" => rusty_firrtl::Stmt::When(cond, rusty_firrtl::Info::default(), stmts_true, Some(stmts_false)),
  "when" <cond: Expr> "symbol"              "indent" <stmts_true: Stmts> "dedent"  => rusty_firrtl::Stmt::When(cond, rusty_firrtl::Info::default(), stmts_true, None),

  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string"> "symbol" <exprs: Exprs> ")" "symbol" <name: Identifier> <info: Info> => rusty_firrtl::Stmt::Printf(Some(name), clk, clk_val, msg, Some(exprs), info),
  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string"> "symbol" <exprs: Exprs> ")" <info: Info> => rusty_firrtl::Stmt::Printf(None, clk, clk_val, msg, Some(exprs), info),
  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string">                         ")" "symbol" <name: Identifier> <info: Info> => rusty_firrtl::Stmt::Printf(Some(name), clk, clk_val, msg, None, info),
  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string">                         ")" <info: Info> => rusty_firrtl::Stmt::Printf(None, clk, clk_val, msg, None, info),

  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string"> "symbol" <exprs: Exprs> ")" "symbol" <name: Identifier>              => rusty_firrtl::Stmt::Printf(Some(name), clk, clk_val, msg, Some(exprs), rusty_firrtl::Info::default()),
  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string"> "symbol" <exprs: Exprs> ")"              => rusty_firrtl::Stmt::Printf(None, clk, clk_val, msg, Some(exprs), rusty_firrtl::Info::default()),
  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string">                         ")" "symbol" <name: Identifier>              => rusty_firrtl::Stmt::Printf(Some(name), clk, clk_val, msg, None, rusty_firrtl::Info::default()),
  "printf" "(" <clk: Expr> "symbol" <clk_val: Expr> "symbol" <msg: "string">                         ")"              => rusty_firrtl::Stmt::Printf(None, clk, clk_val, msg, None, rusty_firrtl::Info::default()),

  "assert" "(" <clk: Expr> "symbol" <pred: Expr> "symbol" <cond: Expr> "symbol" <msg: "string"> ")" "symbol" <name: Identifier> <info: Info> => rusty_firrtl::Stmt::Assert(Some(name), clk, pred, cond, msg, info),
  "assert" "(" <clk: Expr> "symbol" <pred: Expr> "symbol" <cond: Expr> "symbol" <msg: "string"> ")" "symbol" <name: Identifier>              => rusty_firrtl::Stmt::Assert(Some(name), clk, pred, cond, msg, rusty_firrtl::Info::default()),

  "skip"              => rusty_firrtl::Stmt::Skip(rusty_firrtl::Info::default()),
  "skip" <info: Info> => rusty_firrtl::Stmt::Skip(info),

  "stop" "(" <clk: Expr> "symbol" <cond: Expr> "symbol" <x: "int"> ")" "symbol" <name: Identifier>              => rusty_firrtl::Stmt::Stop(name, clk, cond, x, rusty_firrtl::Info::default()),
  "stop" "(" <clk: Expr> "symbol" <cond: Expr> "symbol" <x: "int"> ")" "symbol" <name: Identifier> <info: Info> => rusty_firrtl::Stmt::Stop(name, clk, cond, x, info),

  "mem" <name: Identifier> "symbol" <info: Info> "indent" "datatype" "=>" <tpe: Type> "depth" "=>" <depth: "int"> "read_lat" "=>" <rd_lat: "int"> "write_lat" "=>" <wr_lat: "int"> <ports: MemoryPorts> <ruw: ChirrtlMemoryReadUnderWrite> "dedent" => {
      rusty_firrtl::Stmt::Memory(tpe, depth.to_u32(), rd_lat.to_u32(), wr_lat.to_u32(), ports, ruw, info)
  },
};

pub Ports: rusty_firrtl::Ports = {
    <mut head: Ports> <tail: Port> => { head.push(Box::new(tail)); head } ,
    <head: Port> => vec![Box::new(head)],
};

pub Port: rusty_firrtl::Port = {
    "input"  <name: Identifier> "symbol" <tpe: Type> <info: Info> => rusty_firrtl::Port::Input(name, tpe, info),
    "input"  <name: Identifier> "symbol" <tpe: Type>              => rusty_firrtl::Port::Input(name, tpe, rusty_firrtl::Info::default()),
    "output" <name: Identifier> "symbol" <tpe: Type> <info: Info> => rusty_firrtl::Port::Output(name, tpe, info),
    "output" <name: Identifier> "symbol" <tpe: Type>              => rusty_firrtl::Port::Output(name, tpe, rusty_firrtl::Info::default()),
};

pub Module: rusty_firrtl::Module = {
             "module" <name: Identifier> "symbol" <info: Info> "indent" <ports: Ports> <stmts: Stmts> "dedent" => rusty_firrtl::Module::new(name, ports, stmts, info),
    "public" "module" <name: Identifier> "symbol" <info: Info> "indent" <ports: Ports> <stmts: Stmts> "dedent" => rusty_firrtl::Module::new(name, ports, stmts, info),
             "module" <name: Identifier> "symbol"              "indent" <ports: Ports> <stmts: Stmts> "dedent" => rusty_firrtl::Module::new(name, ports, stmts, rusty_firrtl::Info::default()),
    "public" "module" <name: Identifier> "symbol"              "indent" <ports: Ports> <stmts: Stmts> "dedent" => rusty_firrtl::Module::new(name, ports, stmts, rusty_firrtl::Info::default()),
};

pub DefName: rusty_firrtl::DefName = {
    "defname" "symbol" <name: Identifier> => rusty_firrtl::DefName::from(name),
};

pub Parameter: rusty_firrtl::Parameter = {
    "parameter" <name: Identifier> "symbol" <msg: "string"> => rusty_firrtl::Parameter::StringParam(name, msg),
    "parameter" <name: Identifier> "symbol" <val: Float>    => rusty_firrtl::Parameter::FloatParam(name, val),
    "parameter" <name: Identifier> "symbol" <val: "int">    => rusty_firrtl::Parameter::IntParam(name, val),
};

pub Parameters: rusty_firrtl::Parameters = {
    <mut head: Parameters> <tail: Parameter> => { head.push(Box::new(tail)); head } ,
    <head: Parameter> => vec![Box::new(head)],
};

pub ExtModule: rusty_firrtl::ExtModule = {
    "extmodule" <name: Identifier> "symbol" <info: Info> "indent" <ports: Ports> <defname: DefName> <params: Parameters> "dedent" => rusty_firrtl::ExtModule::new(name, ports, defname, params, info),
    "extmodule" <name: Identifier> "symbol" <info: Info> "indent" <ports: Ports> <defname: DefName>                      "dedent" => rusty_firrtl::ExtModule::new(name, ports, defname, vec![], info),
    "extmodule" <name: Identifier> "symbol"              "indent" <ports: Ports> <defname: DefName> <params: Parameters> "dedent" => rusty_firrtl::ExtModule::new(name, ports, defname, params, rusty_firrtl::Info::default()),
    "extmodule" <name: Identifier> "symbol"              "indent" <ports: Ports> <defname: DefName>                      "dedent" => rusty_firrtl::ExtModule::new(name, ports, defname, vec![], rusty_firrtl::Info::default()),
};

pub Annotations: rusty_firrtl::Annotations = {
    <a: "annotations"> => rusty_firrtl::Annotations::from_str(a)
};

pub Version: rusty_firrtl::Version = {
    "FIRRTL" "version" <a: "int"> "." <b: "id"> "." <c: "id"> => rusty_firrtl::Version(a.to_u32(), b.to_u32(), c.to_u32()),
};

pub CircuitModule: rusty_firrtl::CircuitModule = {
    <m: Module> => rusty_firrtl::CircuitModule::Module(m),
    <em: ExtModule> => rusty_firrtl::CircuitModule::ExtModule(em),
};

pub CircuitModules: rusty_firrtl::CircuitModules = {
    <mut head: CircuitModules> <tail: CircuitModule> => { head.push(Box::new(tail)); head } ,
    <head: CircuitModule> => vec![Box::new(head)],
};

pub Circuit: rusty_firrtl::Circuit = {
                       "circuit" <name: Identifier> "symbol" "indent" <modules: CircuitModules> "dedent" => rusty_firrtl::Circuit::new(rusty_firrtl::Version::default(), name, rusty_firrtl::Annotations::default(), modules),
    <version: Version> "circuit" <name: Identifier> "symbol" "indent" <modules: CircuitModules> "dedent" => rusty_firrtl::Circuit::new(version, name, rusty_firrtl::Annotations::default(), modules),
                       "circuit" <name: Identifier> "symbol" <annos: Annotations> "indent" <modules: CircuitModules> "dedent" => rusty_firrtl::Circuit::new(rusty_firrtl::Version::default(), name, annos, modules),
    <version: Version> "circuit" <name: Identifier> "symbol" <annos: Annotations> "indent" <modules: CircuitModules> "dedent" => rusty_firrtl::Circuit::new(version, name, annos, modules),
}
